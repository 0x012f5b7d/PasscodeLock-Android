buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.novoda:bintray-release:0.9.1'
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'

repositories {
    google()
    jcenter()
    maven { url 'https://maven.google.com' }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.preference:preference:1.0.0'
}

android {
    publishNonDefault true
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 3
        versionName "2.0.1"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("passcodelock.")) {
            buildType.buildConfigField "String", property.key.replace("passcodelock.", "").replace(".", "_").toUpperCase(), "\"${property.value}\""
        }
    }
}

publish {
    userOrg = 'wordpress-mobile'
    groupId = 'org.wordpress'
    uploadName = 'passcodelock'
    artifactId = 'passcodelock'
    desc = 'Android library that provides passcode lock to your app'
    publishVersion = android.defaultConfig.versionName
    licences = ['MIT', 'GPL-2.0']
    website = 'https://github.com/wordpress-mobile/PasscodeLock-Android'
    dryRun = 'false'
    autoPublish = 'true'
}
